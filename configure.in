dnl
dnl Configure script for lifelines software
dnl
dnl Process this file with autoconf to produce a configure script.

AC_INIT(src/liflines/main.c)
AC_PREREQ(2.50)
AC_REVISION([for lifelines, built with autoconf] AC_ACVERSION)
AC_CONFIG_AUX_DIR(build/autotools)
AC_CANONICAL_HOST

AM_INIT_AUTOMAKE(lifelines, 3.0.41d)
AM_CONFIG_HEADER(config.h)

dnl Checks for programs.
AC_PROG_CC
dnl MTE: 01/11/04: Force the use of GNU Bison only.  AC_PROG_YACCj
dnl will find any YACC, but only Bison works for us.
AC_PROG_YACC
if test "$YACC" != "bison -y";
then
  echo "LifeLines requires GNU Bison to compile."
  echo "Please install GNU Bison."
  exit 1
fi
AC_PROG_RANLIB
AC_PROG_INSTALL

dnl Internationalization (gettext)
dnl ALL_LINGUAS is not needed with latest autotools
dnl but debian maintainer still needs it
dnl These languages should be a copy of po/LINGUAS
ALL_LINGUAS="da de el fr it nl pl sv"
AM_GNU_GETTEXT(,need-ngettext)
dnl codeset conversion library
AM_ICONV

dnl Add warning flags when using The GNU C Compiler
if test "${ac_cv_prog_gcc}" = "yes"; then
  # -ansi breaks compile of liflines/askprogram.c [pere 2000-12-31]
  for flag in \
	-W \
	-Wall \
	-Wcast-align \
	-Wmissing-declarations \
	-Wmissing-prototypes \
	-Wreturn-type \
	-Wstrict-prototypes \
	-pedantic
  do
    JAPHAR_GREP_CFLAGS($flag,    [ CFLAGS="$CFLAGS $flag"    ])
  done

  dnl Add debugging as well
  CFLAGS="${CFLAGS} -g"
fi

# OS-Specific Fixups
# Cygwin - fixes linker problems
# Darwin - will pick up Fink-installed headers and libraries
case $host_os in
  *pc-cygwin*)
    CFLAGS="${CFLAGS} -DBROKEN_LINKER"
    ;;  
  *apple-darwin*)
    CPPFLAGS="${CPPFLAGS} -I/sw/include";
    LDFLAGS="${LDFLAGS} -L/sw/lib"
    ;;
esac

# Shall we build the docs?
AC_ARG_WITH(docs,
    [  --with-docs             Build the docs (requires sgmltools) ],
    [with_docs=$withval],
    [with_docs=no]
)

DOCS_TARGET=""
if test "$with_docs" = "yes"
then
   	echo Looking for sgmltools
	AC_PATH_PROG(SGMLTOOLS, sgmltools, FAIL)
	if test "$SGMLTOOLS" = "FAIL"; then
		AC_MSG_RESULT( ******************************************************************* )
		AC_MSG_RESULT( *** Cannot find sgmltools to build docs. skipping building docs *** )
		AC_MSG_RESULT( *** Visit http://sgmltools-lite.sourceforge.net and install it. *** )
		AC_MSG_RESULT( ******************************************************************* )
	else
		DOCS_TARGET="simple"

	fi
fi

AC_SUBST(DOCS_TARGET)

# Compile with profiling, to find bottlenecks
AC_ARG_WITH(profiling,
  [  --with-profiling        Compile with profiling support],
  [# Does it work for other compilers then GCC? [pere 2000-12-30]
   if test "${ac_cv_prog_gcc}" = "yes"; then
      CFLAGS="${CFLAGS} -pg -a"
      LDFLAGS="${LDFLAGS} -pg -a"

      # Make sure 'config.h' is changed if profiling is turned on, to
      # trigger recompile for every source file.
      AC_DEFINE(PROFILING, 1, [Profiling enabled?])
   else
      echo "Do not know how to perform profiling using this compiler!"
   fi
  ]
)

echo Looking for libraries
echo Looking for ncurses
AC_CHECK_LIB(ncurses, tparm)
if test "$ac_cv_lib_ncurses_tparm" = "yes"; then
  echo Using ncurses
else
  echo Looking for curses
  AC_CHECK_LIB(curses, main)
  if test "$ac_cv_lib_curses_main" = "yes"; then
    echo Using curses
  else
    echo "Didn't find an up-to-date ncurses or curses library!"
    echo "Link problems may result!"
  fi
fi

dnl AC_CHECK_LIB(gdbm, gdbm_open)
dnl AC_CHECK_LIB(nsl, inet_ntoa)
dnl AC_CHECK_LIB(socket, getpeername)
dnl AC_CHECK_LIB(resolv, gethostname)
dnl AC_CHECK_LIB(iberty, bzero)
dnl AC_CHECK_LIB(gen, syslog)
dnl AC_CHECK_LIB(bsd, revoke)
dnl AC_CHECK_LIB(ucb, bzero)

echo Looking for header files
AC_CHECK_HEADERS( getopt.h alloc.h malloc.h dirent.h pwd.h locale.h )
dnl AC_HEADER_STDC
dnl AC_CHECK_HEADERS( errno.h dirent.h fcntl.h getopt.h libgen.h libintl.h pwd.h siginfo.h signal.h statbuf.h stdio.h stdlib.h string.h strings.h sys/stat.h sys/time.h sys/types.h unistd.h )
AC_CHECK_HEADERS( wchar.h wctype.h )

dnl echo Looking for typedefs, structures, and compiler characteristics.
dnl AC_C_CONST
dnl AC_HEADER_TIME

echo Looking for library functions
AC_CHECK_FUNCS( _vsnprintf heapwalk _heapwalk getpwuid setlocale )

dnl ngettext function missing from 0.10.35
saved_libs="$LIBS"
LIBS="$LIBS $LIBINTL"
AC_TRY_LINK([#include <libintl.h>], [ngettext("","",0);],
    ac_cv_func_ngettext=yes, ac_cv_func_ngettext=no)
  if test "$ac_cv_func_ngettext" = yes; then
    echo "found ngettext"
    AC_DEFINE(HAVE_NGETTEXT, 1, [Define if you have the ngettext() function.])
  else
    echo "couldn't find ngettext"
    LIBS="$saved_libs"
  fi

dnl bind_textdomain_codeset function missing from 0.10.35
saved_libs="$LIBS"
LIBS="$LIBS $LIBINTL"
AC_TRY_LINK([#include <libintl.h>], [bind_textdomain_codeset("","");],
    ac_cv_func_bind_textdomain_codeset=yes, ac_cv_func_bind_textdomain_codeset=no)
  if test "$ac_cv_func_bind_textdomain_codeset" = yes; then
    echo "found bind_textdomain_codeset"
    AC_DEFINE(HAVE_BIND_TEXTDOMAIN_CODESET, 1, [Define if you have the bind_textdomain_codeset() function.])
  else
    echo "couldn't find bind_textdomain_codeset"
    LIBS="$saved_libs"
  fi

AC_CHECK_FUNCS( wcscoll towlower towupper iswspace iswalpha)

dnl Check for replacement functions
AC_REPLACE_FUNCS( sleep scandir alphasort getopt snprintf vsnprintf \
	_llnull nl_langinfo wcslen has_key )

dnl AC_CHECK_FUNCS(strerror perror strsignal psignal getopt getopt_long random)
dnl investigate using AC_MSG_ERROR for error reporting
dnl echo Looking for OPTIONAL library functions
dnl AC_REPLACE_FUNCS(dirname)

dnl echo Examining library functions
dnl AC_TYPE_SIGNAL

dnl we only use this if we do not have strsignal
dnl AC_DECL_SYS_SIGLIST

AC_OUTPUT(Makefile \
        src/Makefile \
        src/arch/Makefile \
        src/btree/Makefile \
        src/interp/Makefile \
        src/gedlib/Makefile \
        src/stdlib/Makefile \
        src/liflines/Makefile \
        src/tools/Makefile \
	src/ui/Makefile \
        src/hdrs/Makefile \
        src/hdrs/win32/Makefile \
        docs/Makefile \
        reports/Makefile \
        tt/Makefile \
        win32/Makefile \
        build/Makefile \
        intl/Makefile \
        po/Makefile.in)
